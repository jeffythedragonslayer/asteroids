Rem Project: Asteroids
Rem Created: 10/9/2006 3:48:47 PM

Rem ***** Main Source File *****

disable escapekey

rem Standard Setup Code
sync on : sync rate 60
backdrop off : hide mouse: autocam off
set text font "slikscreen" : set text size 12 : set text transparent
randomize timer()

global ship_speed_x as float
global ship_speed_y as float
global ship_x as float
global ship_y as float
global invader_x as float
global invader_y as float
global invader_speed_x as float
global invader_speed_y as float
global invader_cool as integer
global cool as integer
global ship_alive as boolean
global fuel as integer
global ammo as integer
global respawndelay as integer
global hyperspacetime as integer
global lives as integer
global timetoflash as integer
global hyperspacecharge as integer
global hyperspacealreadycharged as integer
global autohyperspace as integer
global levelup as integer
global leveluptime as integer
global level as integer
global score as integer
global dispscore as integer
global gameisover as integer
global invadertime as integer
global pickuptime as integer
global entername as string
global stillon as boolean
global paused as boolean
#constant maxfuel = 400
#constant maxammo = 20
#constant maxbulletlife = 100
#constant maxinvaderbulletlife = 200
#constant guncool = 30
#constant invaderguncool = 150
#constant rocks = 5
#constant respawntime = 100
#constant subdivide = 2
#constant hyperspaceclock = 250
#constant maxlives = 5
#constant hyperspaceconsumption = 700
#constant pickupdelay = 100
set dir "Media"
load sound "Rematerialization.wav", 1
load sound "Bomb near.wav", 2
load sound "Razor Slate.wav", 3
load sound "Large Explosion 1.wav", 4
load sound "Teleport.wav", 5
load sound "Jet loop.wav", 6
load sound "Magic Tree.wav", 7
load sound "Beam.wav", 8
load sound "Space Gen loop 1.wav", 9
load sound "Button 2.wav", 10
load music "music space.mp3", 1
loop music 1

rem load and make the backdrop
load image "backdrop.bmp", 1, 1

rem load and make the ship
set image colorkey 255, 0, 255
load image "ship_thrust.bmp", 2, 1
load image "ship_drift.bmp", 14, 1
load image "ship_retro.bmp", 15, 1
sprite 2, 0, 0, 2
set sprite 2, 0, 1
offset sprite 2, 11.5, 15.5
flip sprite 2

rem make the bullets
load image "bullet1.bmp", 3, 1
load image "bullet2.bmp", 17, 1
type bullet
   id as integer
   life as integer
   speed as float
endtype
dim bullets(-1) as bullet

rem make the asteroids
set image colorkey 0, 255, 0
load image "asteroids.bmp", 4, 1
paste image 4, 0, 0
delete image 4
set image colorkey 255, 0, 255
for count = 0 to 7
   get image count + 4, count * 39.875, 0, (count + 1) * 39.875 - 1, 39, 1
next count

type asteroid
   id as integer
   size as integer
   speed_x as float
   speed_y as float
   x as float
   y as float
endtype
dim asteroids(-1) as asteroid

rem make the explosions
load image "explosion.bmp", 13, 1

type explosion
   id as integer
endtype
dim explosions(-1) as explosion

rem load the invader
load image "Invader.bmp", 16, 1
create animated sprite 3, "Invader.bmp", 2, 1, 16
sprite 3, 0, 0, 16
offset sprite 3, 8, 21.5

rem load the pickups
load image "Life.bmp", 18, 1
load image "Fuel.bmp", 19, 1
load image "Ammo.bmp", 20, 1
load image "Silver.bmp", 21, 1

type pickup
   id as integer
   #constant pickupLife = 1
   #constant pickupFuel = 2
   #constant pickupAmmo = 3
   #constant pickupHyperspace = 4
   kind as integer
endtype

dim pickups(-1) as pickup

init_main_menu()
set dir ".."

load_scores()

main_menu()

restart:

 ship_speed_x = 0
 ship_speed_y = 0
 ship_x = 0
 ship_y = 0
 invader_x = 0
 invader_y = 0
 invader_speed_x = 0
 invader_speed_y = 0
 invader_cool = 0
 cool = 0
 ship_alive = 0
 fuel = 0
 ammo = 0
 respawndelay = 0
 hyperspacetime = 0
 lives = 0
 timetoflash = 0
 hyperspacecharge = 0
 hyperspacealreadycharged = 0
 autohyperspace = 0
 levelup = 0
 leveluptime = 0
 level = 0
 score = 0
 dispscore = 0
 gameisover = 0
 invadertime = 0
 pickuptime = 0


lives = maxlives
invadertime = 100
respawn()
level_up()

` main loop
do
   rem show the backdrop
   paste image 1, 0, 0
   
   if paused = 0

   if gameisover
      if gameover() = 1 then goto restart
   else
      if ship_alive
         update_ship()
      else
         dec respawndelay
         if respawndelay <= 0 then respawn()
      endif
      update_bullets()
      if levelup
         if leveluptime > 0
            dec leveluptime
         else
            level_up()
         endif
      endif
   endif
   if invadertime <= 0
      update_invader()
   else
      dec invadertime
      if invadertime <= 0
         repeat
            invader_speed_x = rnd(2) - 1
            invader_speed_y = rnd(2) - 1
         until invader_speed_x <> 0 or invader_speed_y <> 0
         invader_x = rnd(640)
         invader_y = rnd(480)
         show sprite 3
         sprite 3, invader_x, invader_y, 17
         loop sound 9
      endif
   endif

   update_asteroids()
   update_pickups()
   update_explosions()
   
   
    if keystate(197) = 0
      stillon = 0
    endif
    if stillon = 0
      if keystate(197)
        paused = 1
        stillon = 1
      endif
    endif
   
   else
      ink -1, -1
      center text 320, 240, "Paused"
      if keystate(197) = 0
        stillon = 0
      endif
      if stillon = 0
        if keystate(197) = 1
          paused = 0
          stillon = 1
        endif
      endif
   endif

   rem draw the heads up display before rendering the sprites
   HUD()
   sync

loop
function update_pickups()
   local x as integer
   local y as integer
   local toofull as boolean

   if pickuptime <= 0
      if array count(pickups(0)) < 5
         array insert at bottom pickups(0)
         pickups().id = free_spr()

         rem decide which pickup it should be
         if rnd(5) = 5
            pickups().kind = pickupLife
         else
            if rnd(6) = 6
               pickups().kind = pickupHyperspace
            else
               if rnd(1) = 1
                  pickups().kind = pickupFuel
               else
                  pickups().kind = pickupAmmo
               endif
            endif
         endif

         sprite pickups().id, rnd(640), rnd(480), 17 + pickups().kind
         rotate sprite pickups().id, rnd(360)

         rem get the right offset
         select pickups().kind
            case pickupFuel
               x = 11.5
               y = 5
            endcase
            case pickupLife
               x = 21
               y = 9
            endcase
            case pickupAmmo
               x = 21
               y = 9
            endcase
            case pickupHyperspace
               x = 0
               y = 0
            endcase
         endselect
         offset sprite pickups().id, x, y
      endif
      pickuptime = pickupdelay
   else
      dec pickuptime
   endif

   array index to top pickups(0)
   do
      if array index valid(pickups(0)) = 0 then exitfunction

      move sprite pickups().id, 1

      rem wrap the pickup
      if sprite x(pickups().id) < 0 then sprite pickups().id, 640, sprite y(pickups().id), 17 + pickups().kind
      if sprite x(pickups().id) > 640 then sprite pickups().id, 0, sprite y(pickups().id), 17 + pickups().kind
      if sprite y(pickups().id) < 0 then sprite pickups().id, sprite x(pickups().id), 480, 17 + pickups().kind
      if sprite y(pickups().id) > 480 then sprite pickups().id, sprite x(pickups().id), 0, 17 + pickups().kind

      rem if the pickup hits the ship, get the pickup
      if sprite collision(2, pickups().id) and ship_alive and hyperspacetime <= 0
         select pickups().kind
            case pickupFuel
               if fuel => maxfuel
                  toofull = 1
               else
                  inc fuel, 150
                  play sound 10
                  if fuel > maxfuel then fuel = maxfuel
               endif
            endcase
            case pickupLife
               if lives => maxlives - 1
                  toofull = 1
               else
                  inc lives
                  play sound 10
               endif
            endcase
            case pickupAmmo
               if ammo => maxammo
                  toofull = 1
               else
                  inc ammo, 10
                  play sound 10
                  if ammo > maxammo then ammo = maxammo
               endif
            endcase
            case pickuphyperspace
               if hyperspacecharge => hyperspaceconsumption
                  toofull = 1
               else
                  hyperspacecharge = hyperspaceconsumption
                  hyperspacealreadycharged = 0
               endif
            endcase
         endselect
         if toofull
            next array index pickups(0)
            toofull = 0
         else
            delete sprite pickups().id
            array delete element pickups().id
         endif
      else
         next array index pickups(0)
      endif

   loop
endfunction
function update_invader()

      rem time to fly across the screen
      play sprite 3, 1, 2, 100
      inc invader_x, invader_speed_x
      inc invader_y, invader_speed_y
      sprite 3, invader_x, invader_y, 16

      rem wrap the invader
      if invader_x < 0 then invader_x = 640
      if invader_x > 640 then invader_x = 0
      if invader_y < 0 then invader_y = 480
      if invader_y > 480 then invader_y = 0

      dec invader_cool
      if invader_cool <= 0 and ship_alive and hyperspacetime <= 50
         rem time to shoot
         invader_cool = invaderguncool

         array insert at bottom bullets(0)
         bullets().id = free_spr()
         bullets().life = maxinvaderbulletlife

         sprite bullets().id, invader_x, invader_y, 17
         offset sprite bullets().id, 3.5, 3.5
         rotate sprite bullets().id, 180 - atanfull(sprite x(2) - sprite x(3), sprite y(2) - sprite y(3))

         rem make the bullet start moving
         bullets().speed = 3
         move sprite bullets().id, 20

         play sound 8

      endif
endfunction
function HUD()
   local floatingpoint as float

   rem level
   ink -1, 0
   center text 320, 10, "Level " + str$(level)

   rem score
   if dispscore < score - 200
      inc dispscore, 7
   else
      if dispscore < score then inc dispscore
   endif
   text 10, 10, "Score: " + cstr$(dispscore)

   rem lives
   for count = 1 to lives
      paste image 14, 630 - (count * 35) , 10, 1
   next count

   rem ammo
   if ammo
      text 10, 440, "Ammo: " + str$(ammo)
   else
      ink rgb(255, 0, 0), 0
      text 10, 440, "No Ammo"
   endif

   rem fuel
   if fuel
      ink -1, 0
      text 10, 460, "Fuel"
      box 40, 460, 40 + fuel, 470, rgb(255, 0, 0),  rgb(255, 0, 0),  rgb(0, 255, 0),  rgb(0, 255, 0)
   else
      ink rgb(255, 0, 0), 0
      text 10, 460, "No Fuel"
   endif

   rem hyperspace
   ink rgb(0, 0, 255), 0
   if hyperspacecharge => hyperspaceconsumption
      box 595, 470 - (230 - text height("hyperspace")), 605, 470, rgb(255, 0, 0), rgb(0, 0, 255), rgb(255, 0, 0), rgb(0, 0, 255)
      if hyperspacealreadycharged = 0 then play sound 7
      hyperspacealreadycharged = 1
   else
      floatingpoint = hyperspacecharge
      box 595, 470 - (floatingpoint / hyperspaceconsumption) * (230 - text height("hyperspace")), 605, 470, rgb(255, 0, 0), rgb(0, 0, 255), rgb(255, 0, 0), rgb(0, 0, 255)
      ink rgb(255, 0, 0), 0
   endif
   text 630 - text width("hyperspace"), 230, "hyperspace"

   dec autohyperspace
   if autohyperspace => 0
      ink -1, 0
      center text 320, 240, "Auto Hyperspace Engaged"
   endif

endfunction
function gameover()

   entername = entry$(1)

   ink -1, 0
   dispscore = score
   center text 320, 240, "Game Over"
   center text 320, 270, "Enter your name:"
   if (timer()/500) mod 2 = 1
     text 290, 290, entername + "_"
   else
     text 290, 290, entername
   endif

   if right$(entername, 1) = ","
    clear entry buffer
    entername = ""
   endif

   if returnkey() and len(entername) > 1
     entername = left$(entername, len(entername) - 1)
     
     array insert at bottom hiscores(0)
     hiscores().player = entername
     hiscores().score = score
     hiscores().time = get time$()
     hiscores().date = get date$()
     sort()
     save_scores()
     main_menu()
     exitfunction 1
   endif
endfunction 0
function respawn()
   if lives = 0
      gameisover = 1
      clear entry buffer
      exitfunction
   endif
   play sound 1
   dec lives
   fuel = maxfuel
   ammo = maxammo
   ship_alive = 1
   ship_x = 320
   ship_y = 240
   ship_speed_x = 0
   ship_speed_y = 0
   show sprite 2
   rotate sprite 2, 0
   sprite 2, ship_x, ship_y, 14
   hyperspacetime = hyperspaceclock - 50
   hyperspacecharge = 0
   set sprite alpha 2, 64
endfunction
function hyperspace()
   if hyperspacecharge < hyperspaceconsumption or hyperspacetime then exitfunction
   hyperspacetime = hyperspaceclock
   set sprite alpha 2, 64
   hyperspacecharge = 0
   hyperspacealreadycharged = 0
   play sound 5
endfunction
function update_ship()
   local movenow as boolean

   rem hyperspace
   if controlkey() then hyperspace()

   rem rotate ship
   rotate sprite 2, sprite angle(2) + (rightkey() - leftkey()) * 3

   rem control ship
   set sprite image 2, 14
   if fuel > 0
      if upkey()
         dec fuel
         inc ship_speed_x, -cos(90 - sprite angle(2)) / 10
         inc ship_speed_y, cos(sprite angle(2)) / 10
         set sprite image 2, 2
         movenow = 1
      endif
      if downkey()
         dec fuel
         dec ship_speed_x, -cos(90 - sprite angle(2)) / 10
         dec ship_speed_y, cos(sprite angle(2)) / 10
         set sprite image 2, 15
         movenow = 1
      endif
      if movenow = 1
         if sound looping(6) = 0 then loop sound 6
      else
         stop sound 6
      endif
   else
      if sound looping(6) then stop sound 6
   endif

   rem move ship
   inc ship_x, ship_speed_x
   inc ship_y, ship_speed_y

   rem wrap the ship
   if ship_x < 0 then ship_x =  640
   if ship_x > 640 then ship_x = 0
   if ship_y < 0 then ship_y = 480
   if ship_y > 480 then ship_y = 0

   rem update ship position
   sprite 2, ship_x, ship_y, sprite image(2)

   rem shoot bullets
   if hyperspacetime <= 0
      dec cool
      inc hyperspacecharge
      if spacekey() and cool <= 0 and ammo > 0
         cool = guncool
         dec ammo

         array insert at bottom bullets(0)
         bullets().id = free_spr()
         bullets().life = maxbulletlife

         sprite bullets().id, ship_x, ship_y, 3
         offset sprite bullets().id, 3.5, 3.5
         rotate sprite bullets().id, 180 + sprite angle(2)

         rem make the bullet start moving
         bullets().speed = ship_speed_x + ship_speed_y + 10
         move sprite bullets().id, 30

         play sound 3
      endif

      rem if the ship hits the invader, explode the ship and the invader
      if sprite collision(2, 3)
         rem only get 100 points because it was a kamakaize attack
         explode_invader(100)
         explode_ship()
      endif
   else
      dec hyperspacetime
      if hyperspacetime <= 0
         set sprite alpha 2, 255
      else
         if hyperspacetime < 100
            if timetoflash <= 0
               if sprite alpha(2) = 255
                  set sprite alpha 2, 64
               else
                  set sprite alpha 2, 255
               endif
               timetoflash = 2
            endif
            dec timetoflash
         endif
      endif
   endif

endfunction
function update_bullets()
   rem move bullets
   array index to top bullets(0)
   do
      if array index valid(bullets(0)) = 0 then exit

      rem if the bullets run out of life, make them dissapear
      dec bullets().life
      if bullets().life <= 0
         rem make bullet dissapear
         delete sprite bullets().id
         array delete element bullets(0)
      else
         rem move the bullet
         move sprite bullets().id, bullets().speed

         rem wrap the bullet
         if sprite x(bullets().id) < 0 then sprite bullets().id, 640, sprite y(bullets().id), sprite image(bullets().id)
         if sprite x(bullets().id) > 640 then sprite bullets().id, 0, sprite y(bullets().id), sprite image(bullets().id)
         if sprite y(bullets().id) < 0 then sprite bullets().id, sprite x(bullets().id), 480 , sprite image(bullets().id)
         if sprite y(bullets().id) > 480 then sprite bullets().id, sprite x(bullets().id), 0, sprite image(bullets().id)

         rem check if the bullet is hitting anything
         bullet_collision()

         next array index bullets(0)
      endif

   loop
endfunction
function bullet_collision()
   rem check each asteroid and see if it's colliding with the bullet
   array index to top asteroids(0)
   do
      if array index valid(asteroids(0)) = 0 then exit

      if sprite collision(bullets().id, asteroids().id)
         explode_asteroid()
         exitfunction
      endif

      next array index asteroids(0)
   loop

   rem if it hits the ship, explode the ship
   if ship_alive
      if hyperspacetime <= 0
         if sprite collision(bullets().id, 2)
            explode_ship()
            delete sprite bullets().id
            array delete element bullets(0)
            exitfunction
         endif
      endif
   endif

   rem if it hits the invader, make the invader explode
   if invadertime <= 0 and sprite collision(bullets().id, 3) and sprite image(bullets().id) <> 17
      explode_invader(200)
      delete sprite bullets().id
      array delete element bullets(0)
      exitfunction
   endif

endfunction
function explode_invader(points as boolean)
   invadertime = (2000 / level) + rnd(400)
   inc score, points
   make_explosion(invader_x, invader_y)
   hide sprite 3
   play sound 4
   stop sound 9
endfunction
function update_asteroids()
   rem move asteroids
   array index to top asteroids(0)
   do
      if array index valid(asteroids(0)) = 0 then exit

      rem rotate the asteroid
      rotate sprite asteroids().id, sprite angle(asteroids().id) + 1

      rem move the asteroid
      inc asteroids().x, asteroids().speed_x
      inc asteroids().y, asteroids().speed_y

      rem wrap the asteroid
      if asteroids().x < 0 then asteroids().x =  640
      if asteroids().x > 640 then asteroids().x = 0
      if asteroids().y < 0 then asteroids().y = 480
      if asteroids().y > 480 then asteroids().y = 0

      rem update asteroid position
      sprite asteroids().id, asteroids().x, asteroids().y, sprite image(asteroids().id)

      rem if the asteroid hits the ship, make the ship explode
      if ship_alive
         if hyperspacetime <= 0
            if sprite collision(asteroids().id, 2)
               if sprite_distance(asteroids().id, 2) < 225
                  explode_ship()
               endif
            endif
         endif
      endif
      if invadertime <= 0
         if sprite collision(asteroids().id, 3)
            explode_invader(0)
         endif
      endif

      next array index asteroids(0)

   loop
endfunction
function explode_asteroid()
   local size as integer
   local x as integer
   local y as integer
   rem explode the asteroid in question, and make two smaller ones
   size = asteroids().size
   x = asteroids().x
   y = asteroids().y
   delete sprite asteroids().id
   array delete element asteroids(0)

   rem if the bullet was from the player, give points
   if sprite image(bullets().id) = 3 then inc score, size

   rem make the bullet dissapear
   delete sprite bullets().id
   array delete element bullets()

   play sound 2

   rem make two smaller asteroids only if they will be big enough
   if size > 50
      for count = 1 to subdivide
         array insert at bottom asteroids()
         asteroids().id = free_spr()
         asteroids().size = (size / 3) * 2

         rem give the new asteroid the same coordiantes of the bigger one but a different direction
         asteroids().x = x
         asteroids().y = y
         sprite asteroids().id, asteroids().x, asteroids().y, rnd(7) + 4
         offset sprite asteroids().id, 17.5, 17
         scale sprite asteroids().id, asteroids().size

         this_way = rnd(360)
         asteroids().speed_x = sin(this_way) * (1 + (level / 2))
         asteroids().speed_y = cos(this_way) * (1 + (level / 2))

      next count
   else
      if array count(asteroids(0)) = -1
         levelup = 1
         leveluptime = 100
      endif
   endif



   rem make an explosion
   make_explosion(x, y)

endfunction
function update_explosions()
   array index to top explosions()
   do
      if array index valid(explosions(0)) = 0 then exit

      rem play the animation
      play sprite explosions().id, 1, 15, 40

      rem make the explosion dissapear when it's done
      if sprite frame(explosions().id) = 15
         delete sprite explosions().id
         array delete element explosions()
      else
         next array index explosions()
      endif
   loop

endfunction
function level_up()
   local this_way as float

   delete_bullets()

   levelup = 0
   ship_x = 320
   ship_y = 240
   ship_speed_x = 0
   ship_speed_y = 0
   hyperspacetime = hyperspaceclock
   sprite 2, ship_x, ship_y, 14
   set sprite alpha 2, 64

   rem make some asteroids
   empty array asteroids(0)
   for count = 1 to rocks

      array insert at bottom asteroids(0)
      asteroids().id = free_spr()
      asteroids().size = 100

      asteroids().x = rnd(640)
      asteroids().y = rnd(480)
      sprite asteroids().id, asteroids().x, asteroids().y, rnd(7) + 4
      offset sprite asteroids().id, 17.5, 17
      scale sprite asteroids().id, asteroids().size

      this_way = rnd(360)
      asteroids().speed_x = sin(this_way) * (1 + (level / 2))
      asteroids().speed_y = cos(this_way) * (1 + (level / 2))

   next count

   inc level

endfunction
function delete_bullets()
   array index to top bullets(0)
   do
      if array index valid(bullets(0)) = 0 then exitfunction

      delete sprite bullets().id
      array delete element bullets()

   loop
endfunction
function make_explosion(x as integer, y as integer)
   rem make an explosion
   array insert at bottom explosions(0)
   explosions().id = free_spr()
   set dir "Media"
   create animated sprite explosions().id, "explosion.bmp", 16, 1, 13
   set dir ".."
   sprite explosions().id, x, y, 13
   set sprite explosions().id, 0, 1
   offset sprite explosions().id, 47.9375, 23.5
   rotate sprite explosions().id, rnd(360)
endfunction
function explode_ship()
   rem auto hyperspace
   if hyperspacecharge => hyperspaceconsumption
      autohyperspace = 200
      hyperspace()
      exitfunction
   endif
   ship_alive = 0
   respawndelay = respawntime
   make_explosion(ship_x, ship_y)
   hide sprite 2
   play sound 4
   stop sound 6
endfunction
function sprite_distance(sprite1 as integer, sprite2 as integer)
   local distance as integer
   distance = (sprite x(sprite1) - sprite x(sprite2)) ^ 2 + (sprite y(sprite1) - sprite y(sprite2))
endfunction distance

Rem *** Include File: Menu.dba ***
Rem Created: 10/29/2006 9:50:27 AM

Rem Included inProject: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\Asteroids\Asteroids.dbpro

global img_cursor as dword
global spr_cursor as dword
global spr_play as dword
global img_play as dword
global spr_quit as dword
global img_quit as dword
global spr_scores as dword
global img_scores as dword

function init_main_menu()

  img_play = load_img("Play.jpg", 1)
  img_scores = load_img("Hi Scores.jpg", 1)
  img_quit = load_img("Quit.jpg", 1)

endfunction

function main_menu()

   hide all sprites
   cls rgb(255, 0, 255)

   spr_play = make_spr(0, 0, img_play)
   sprite spr_play, 320 - sprite width(spr_play) / 2, 200, img_play
   set sprite alpha spr_play, 80
   
   spr_scores = make_spr(0, 0, img_scores)
   sprite spr_scores, 320 - sprite width(spr_scores) / 2, 300, img_scores
   set sprite alpha spr_scores, 80
   
   spr_quit = make_spr(0, 0, img_quit)
   sprite spr_quit, 320 - sprite width(spr_quit) / 2, 400, img_quit
   set sprite alpha spr_quit, 80

   ink rgb(255, 0, 0), 0
   circle 5, 5, 5
   line 5, 0, 5, 10
   line 0, 5, 10, 5
   img_cursor = get_img(0, 0, 11, 11, 1)
   spr_cursor = make_spr(0, 0, img_cursor)

   set text size 64

   local green as byte
   local red as byte
   local blue as byte
   local rway as integer
   local bway as integer
   local cway as integer

   green = rnd(200) + 20
   red = rnd(200) + 40
   blue = rnd(200) + 20
   rway = 1
   gway = 1
   bway = 1

   do
      `move cursor
      sprite spr_cursor, mousex(), mousey(), img_cursor
      if sprite x(spr_cursor) + sprite width(spr_cursor) > 640 then sprite spr_cursor, 640 - sprite width(spr_cursor), sprite y(spr_cursor), img_cursor
      if sprite y(spr_cursor) + sprite height(spr_cursor) > 480 then sprite spr_cursor, sprite x(spr_cursor), 480 - sprite height(spr_cursor), img_cursor


      ` highlight selections
      
      `play
      if sprite_collision(spr_cursor, spr_play)
         set sprite alpha spr_play, 160
         if mouseclick()
            hide_menu1()
            exit
         endif
      else
         set sprite alpha spr_play, 80
      endif
      
      `hi scores
      if sprite_collision(spr_cursor, spr_scores)
         set sprite alpha spr_scores, 160
         if mouseclick()
            hide_menu2()
            scores_menu()
            show_menu2()
         endif
      else
         set sprite alpha spr_scores, 80
      endif

      `quit
      if sprite_collision(spr_cursor, spr_quit)
         if mouseclick() then end
         set sprite alpha spr_quit, 160
      else
         set sprite alpha spr_quit, 80
      endif

      paste image 1, 0, 0

      ` colors
      if red = 255 and rway = 1 then rway = -1
      if red = 0 and rway = -1 then rway = 1
      if green = 255 and gway = 1 then gway = -1
      if green = 0 and gway = -1 then gway = 1
      if blue = 255 and bway = 1 then bway = -1
      if blue = 0 and bway = -1 then bway = 1

      inc red, rway
      inc green, gway
      inc blue, bway


      ink rgb(red, green, blue), -1
      center text screen width() / 2, 100, "Asteroids"
      sync
   loop

endfunction

function hide_menu1()
  delete sprite spr_cursor
  delete sprite spr_play
  delete sprite spr_scores
  delete sprite spr_quit
  set text size 16
endfunction

function hide_menu2()
  hide sprite spr_cursor
  hide sprite spr_play
  hide sprite spr_scores
  hide sprite spr_quit
  set text size 16
endfunction

function show_menu2()
  show sprite spr_cursor
  show sprite spr_play
  show sprite spr_scores
  show sprite spr_quit
  set text size 64
endfunction

function sprite_collision(move, stay)
`calculate center
x = sprite x(move)
y = sprite y(move)
if x > sprite x(stay)
  if x < sprite x(stay) + sprite width(stay)
    if y > sprite y(stay)
      if y < sprite y(stay) + sprite height(stay)
        exitfunction 1
      endif
    endif
  endif
endif
endfunction 0
Rem *** Include File: Hi Scores.dba ***
Rem Created: 11/4/2006 9:27:47 PM

Rem Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\Asteroids\Asteroids.dbpro

type hiscore
   score as dword
   player as string
   date as string
   time as string
endtype

function load_scores()
local str as string

`make the hi scores array
dim hiscores(-1) as hiscore

empty array hiscores(0)

`make the file if it doesn't exist
if file exist("Hi Scores.ini") = 0 then exitfunction

`load the array
open to read 1, "Hi Scores.ini"

do
    read string 1, str
    csv$(str)
    
    if len(str) < 3 then exit

    array insert at bottom hiscores(0)

    hiscores().score = val(csv$(""))
    hiscores().player = csv$("")
    hiscores().date = csv$("")
    hiscores().time = csv$("")
     
loop

close file 1

sort()

endfunction


function save_scores()
local count as dword

`save the array
if file exist("Hi Scores.ini") then delete file "Hi Scores.ini"
open to write 1, "Hi Scores.ini"

array index to top hiscores(0)

for count = 0 to array count(hiscores(0))
    write string 1, str$(hiscores(count).score) + "," + hiscores(count).player + "," + hiscores(count).date + "," + hiscores(count).time
next count

close file 1

endfunction

function scores_menu()

  ink -1, -1

  do
    cls
    paste image 1, 0, 0
  
    if spacekey() then exitfunction
    
    dec cstart, (mousemovez() / 120)
    if cstart < 0
      cstart = 0
    else
      if array count(hiscores(0)) <= 11
        cstart = 0
      else
        if cstart + 11 > array count(hiscores(0))
          cstart = array count(hiscores(0)) - 11
        endif
      endif
    endif
    
    set text size 32
    center text 280, 10, "Hi Scores"
    set text size 16
    
    text 20, 40, "Player"
    text 210, 40, "Score"
    text 300, 40, "Date"
    text 450, 40, "Time"
    text 550, 40, "Rank"
    
    for count = 0 to 11
      ncount = count + cstart
      if ncount => 0 and ncount <= array count(hiscores(0))
        rcount = array count(hiscores(0)) - ncount
        text 20, 60 + count * text height("2") * 1.5, hiscores(rcount).player
        text 210, 60 + count * text height("2") * 1.5, cstr$(hiscores(rcount).score)
        text 300, 60 + count * text height("2") * 1.5, readable_date$(hiscores(rcount).date)
        text 450, 60 + count * text height("2") * 1.5, readable_time$(hiscores(rcount).time)
        text 550, 60 + count * text height("2") * 1.5, str$(ncount + 1)
      endif
    next count
    
    text 20, 400, "Press space to return to the main menu."
    
    sync
  loop
endfunction

function sort()
  if array count(hiscores(0)) > 0
    if array count(hiscores(0)) = 1
      if hiscores(0).score > hiscores(1).score
        swap_scores(0, 1)
      endif
    else
      quicksort(0, array count(hiscores(0)))
    endif
  endif
endfunction

function quicksort(left as integer, right as integer)
   local i as integer
   local j as integer
   local x as dword
   local y as dword
   local str as string

   i = left
   j = right
   x = hiscores((left + right)/2).score
   repeat
      while i<right and i < array count(hiscores())
         if hiscores(i).score >= x then exit
         inc i
      endwhile
      while j>left and j > -1
        if x >= hiscores(j).score then exit
        dec j
      endwhile
      if i<=j
        swap_scores(i, j)
        inc i
        dec j
      endif
   until i > j

   if left < j then quicksort(left, j)
   if i < right then quicksort(i, right)
endfunction

function swap_scores(i as dword, j as dword)
  local y as dword
  local str as string
  
  y = hiscores(i).score
  hiscores(i).score = hiscores(j).score
  hiscores(j).score = y
  str = hiscores(i).player
  hiscores(i).player = hiscores(j).player
  hiscores(j).player = str
  str = hiscores(i).date
  hiscores(i).date = hiscores(j).date
  hiscores(j).date = str
  str = hiscores(i).time
  hiscores(i).time = hiscores(j).time
  hiscores(j).time = str
endfunction
Rem *** Include File: Library.dba ***
Rem Created: 10/29/2006 9:20:09 AM

Rem Included in Project: C:\Program Files\The Game Creators\Dark Basic Professional\Projects\Asteroids\Asteroids.dbpro

` finds an unused sprite
function free_spr()
   local id as dword
   repeat
      inc id
   until sprite exist(id) = 0
endfunction id

` finds an unused image
function free_img()
   local id as dword
   repeat
      inc id
   until image exist(id) = 0
endfunction id

` Converts a number into a string with commas separating the thousands
function cstr$(num as dword)
   local str as string
   local length as dword
   local pos as integer
   str = str$(num)
   length = len(str)
   for pos = length - 3 to 1 step - 3
      str = left$(str, pos) + "," + right$(str, length - pos)
      inc length
   next pos
endfunction str

rem performes a GET IMAGE command and returns the id number
function get_img(Left as dword, Top as dword, Right as dword, Bottom as dword, textureflag as boolean)
   local id as dword
   id = free_img()
   get image id, Left, Top, Right, Bottom, textureflag
endfunction id

rem makes a sprite and returns the id number
function make_spr(xpos as integer, ypos as integer, image as dword)
   local id as dword
   id = free_spr()
   sprite id, xpos, ypos, image
endfunction id

rem loads an image and returns the id number
function load_img(file as string, textureflag as boolean)
   local id as dword
   id = free_img()
   load image file, id, textureflag
endfunction id

` returns an easy to read form of the date
function readable_date$(date_str as string)
   local day_str as string
   local month_str as string
   local month_num as dword
   local year_str as string

   ` is date_str in mm/dd/yyyy or mm/dd/yy form?
   if len(date_str) = 8
      year_str = "20" + right$(date_str, 2)
   else
      year_str = right$(date_str, 4)
   endif

   month_num = val(left$(date_str, 2))
   select month_num
      case 1
         month_str = "January"
      endcase
      case 2
         month_str = "February"
      endcase
      case 3
         month_str = "March"
      endcase
      case 4
         month_str = "April"
      endcase
      case 5
         month_str = "May"
      endcase
      case 6
         month_str = "June"
      endcase
      case 7
         month_str = "July"
      endcase
      case 8
         month_str = "August"
      endcase
      case 9
         month_str = "September"
      endcase
      case 10
         month_str = "October"
      endcase
      case 11
         month_str = "November"
      endcase
      case 12
         month_str = "December"
      endcase
   endselect

   day_str = str$(val(right$(left$(date_str, 5), 2)))

   date_str = month_str + " " + day_str + ", " + year_str

endfunction date_str

function readable_time$(time_str as string)
   local hour_num as dword
   local hour_str as string
   local minute_str as string
   local second_str as string
   local am_pm as string

   ` is time_str in hh:mm:ss form or hh:mm form?
   if len(time_str) = 8
      second_str = " (" + right$(time_str, 2) + ")"
   endif

   minute_str = right$(left$(time_str, 5), 2)

   hour_num = val(left$(time_str, 2))
   if hour_num > 12
      dec hour_num, 12
      am_pm = "pm"
   else
      am_pm = "am"
   endif
   hour_str = str$(hour_num)

   time_str = hour_str + ":" + minute_str + " " + am_pm + second_str

endfunction time_str

` Extracts individual strings from a comma separated values string
` Call with comma separated values string, then call with null string in a loop and it
` will return indivual strings and then "END OF STRING"
global csv_start as dword
global csv_values as string
function csv$(called_values as string)

   if len(called_values)
      csv_values = called_values
      csv_start = 1
      exitfunction ""
   endif

   local length as dword
   length = len(csv_values)

   if csv_start > length then exitfunction "END OF STRING"

   local finish as dword
   local return_value as string

   for finish = csv_start to length
      if mid$(csv_values, finish) = ","
         return_value = right$(left$(csv_values, finish - 1), finish - csv_start)
         csv_start = finish + 1
         exitfunction return_value
      endif
   next finish

   return_value = right$(csv_values, finish - csv_start)
   csv_start = finish
   break
endfunction return_value

` Removes leading and trailing spaces
remstart
function trim$(str as string)
   local pos as dword
   local length as dword
   length = len(str)
   pos = length

   while pos
      if mid$(str, pos) <> " "
         str = left$(str, pos)
         `this is where the exitfunction would be, but instead the ltrim$()
         ` the length must be resized because the string was just chopped
         length = pos
         pos = 1
         while pos <= length
            if mid$(str, pos) <> " "
               dec pos
               str = right$(str, length - pos)
               exitfunction str
            endif
            inc pos
         endwhile
      endif
      dec pos
   endwhile
endfunction ""
remend